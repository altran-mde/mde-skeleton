/*******************************************************************************
 * Copyright (c) 2011, 2019 itemis AG (http://www.itemis.eu) and others.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.xtext.example.fowlerdsl.GenerateStatemachine


import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.eclipse.xtext.example.fowlerdsl"
				rootPath = rootPath
				runtimeTest = {
				    root = "../../tests/org.eclipse.xtext.example.fowlerdsl.tests"
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
                    root = "../../tests/org.eclipse.xtext.example.fowlerdsl.ui.tests"
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				preferXtendStubs = false
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext\n */"
			}
		}
//tag::doc-espilce-polvi[]
		language = StandardLanguage {
			name = "org.eclipse.xtext.example.fowlerdsl.Statemachine"
			fileExtensions = "statemachine,statemachinelsp"

			generator = org.espilce.polvi.xtext.generator.PolviGeneratorFragment {
				generateXtendStub = true
			}
//end::doc-espilce-polvi[]
            formatter = {
                generateStub = true
                generateXtendStub = true
            }
			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				generateDeprecationValidation = false
			}
			projectWizard = {
				generate = true
				generateToolbarButton = true
			}
			fileWizard = {
				generate = true
				generateToolbarButton = true
			}
		}
	}
}